
record Message = [int ID, real SEED, int COUNT];

fun list<Message> removeFirstElement(list<Message> pending){
	list<Message> result = newList(Message);
	if (size(pending) == 1){
		return result;
	}
	for i from 1 to size(pending){
		result = result + [:pending[i]:];
	}
	return result;
}

fun Message createElement(int id, real seed, int counter){
	Message result := [ID := id, SEED := seed, COUNT := counter];
	return result;
}

fun list<Message> addMessage(list<Message> listy, Message message){
	listy = listy + [:message:];
	return listy;
}

const NODEZ = 50;


component Agent(int zone, real seed){
	store{
		attrib int id = zone;
		attrib real seed = seed;
		attrib int sizy = 1;
		attrib zone := zone;
		attrib right := (zone+1)%NODEZ;
		attrib left := (zone+NODEZ-1)%NODEZ;
		attrib list<Message> pending = newList(Message);
		attrib Message toForward = [ID := -1, SEED := -1.0, COUNT = -1];
		attrib int leader = MAXINT;
		attrib real leaderSeed = MAXREAL;
		attrib bool known = false;
		attrib bool done = false;
		
	}
	behaviour{
		
		IDLE = 
			activate*{}.ACTIVEIDLE
			
			+
			
			send[my.left == zone](anId,aSeed,aCounter){
				toForward = createElement(anId,aSeed,aCounter);
				pending = addMessage(pending,toForward);
			}.FOLLOWER;
		
		
		ACTIVEIDLE = 
			
			send[my.right == zone]<my.id,my.seed,1>{
				my.leader = my.id;
				my.leaderSeed = my.seed;
			}.ACTIVE
			
			+
			
			send[my.left == zone](anId,aSeed,aCounter){
				toForward = createElement(anId,aSeed,aCounter);
				pending = addMessage(pending,toForward);
			}.ACTIVEIDLE;
		
		
		ACTIVE =
		
			send[my.left == zone](anId,aSeed,aCounter){
				toForward = createElement(anId,aSeed,aCounter);
				pending = addMessage(pending,toForward);
			}.ACTIVE
			
			+
			
			[
				(size(my.pending) > 0) && 
				(pending[0].ID == my.id)
			]
			check*{
				my.known = true;
			}.CHECK
			
			+
			
			[
				(size(my.pending) > 0) && 
				(pending[0].ID != my.id) && 
				known
			]
			check*{}.CHECK
			
			+
			
			[
				(size(my.pending) > 0) && 
				(pending[0].ID != my.id) && 
				pending[0].SEED < my.leaderSeed
			]
			send[my.right == zone]<pending[0].ID,pending[0].SEED,(pending[0].COUNT+1)>{
				my.leader = pending[0].ID;
				my.leaderSeed = pending[0].SEED;
				my.sizy = my.sizy+1;
				pending = removeFirstElement(pending);
			}.ACTIVE
			
			+
			
			[
				(size(my.pending) > 0) && 
				(pending[0].ID != my.id) && 
				pending[0].SEED > my.leaderSeed
			]
			send[my.right == zone]<pending[0].ID,pending[0].SEED,(pending[0].COUNT+1)>{
				my.sizy = my.sizy+1;
				pending = removeFirstElement(pending);
			}.ACTIVE
			
			+
			
			[
				(size(my.pending) > 0) && 
				pending[0].ID != my.id && 
				pending[0].SEED == my.leaderSeed && 
				(pending[0].ID < my.leader)
			]
			send[my.right == zone]<pending[0].ID,pending[0].SEED,(pending[0].COUNT+1)>{
				my.leader = pending[0].ID;
				my.sizy = my.sizy+1;
				pending = removeFirstElement(pending);
			}.ACTIVE
			
			+
			
			[
				(size(my.pending) > 0) && 
				pending[0].ID != my.id && 
				pending[0].SEED == my.leaderSeed && 
				(pending[0].ID > my.leader)
			]
			send[my.right == zone]<pending[0].ID,pending[0].SEED,(pending[0].COUNT+1)>{
				my.sizy = my.sizy+1;
				pending = removeFirstElement(pending);
			}.ACTIVE;
			
		
		
		CHECK = 
			
			[
				my.sizy == my.pending[0].COUNT && 
				my.leader == my.id
			]
			becomeLeader*{
				pending = removeFirstElement(pending);
			}.LEADER
			
			+
			
			[
				my.sizy == my.pending[0].COUNT && 
				my.leader != my.id
			]
			becomeFollower*{
				pending = removeFirstElement(pending);
			}.FOLLOWER
			
			+
			
			[my.sizy != my.pending[0].COUNT]becomeFeederz*{}.ACTIVE
			
			+
			
			send[my.left == zone](anId,aSeed,aCounter){
				toForward = createElement(anId,aSeed,aCounter);
				pending = addMessage(pending,toForward);
			}.CHECK;
			
			
		LEADER = 
		
			[!done]send[my.right == zone]<my.id,my.seed,1>{
				done = true;
			}.LEADER
			
			+
			
			[done]send[my.left == zone](anId,aSeed,aCounter){
				my.sizy = aCounter;
			}.LEADER;
		
		FOLLOWER =
			
			[
				size(my.pending) > 0 && 
				pending[0].ID == my.leader
			]
			send<pending[0].ID,pending[0].SEED,pending[0].COUNT+1>{
				pending = removeFirstElement(pending);
			}.FOLLOWER
			
			+
		
			send[my.left == zone](anId,aSeed,aCounter){
				toForward = createElement(anId,aSeed,aCounter);
				pending = addMessage(pending,toForward);
			}.FOLLOWER
			
			+
			
			[
				(size(my.pending) > 0) && 
				(pending[0].SEED < my.leaderSeed)
			]
			send[my.right == zone]<pending[0].ID,pending[0].SEED,(pending[0].COUNT)>{
				my.leader = pending[0].ID;
				my.leaderSeed = pending[0].SEED;
				pending = removeFirstElement(pending);
			}.FOLLOWER
			
			+
			
			[
				(size(my.pending) > 0) && 
				(pending[0].SEED > my.leaderSeed)
			]
			send[my.right == zone]<pending[0].ID,pending[0].SEED,(pending[0].COUNT)>{
				pending = removeFirstElement(pending);
			}.FOLLOWER
			
			+
			
			[
				(size(my.pending) > 0) && 
				(pending[0].SEED == my.leaderSeed) && 
				pending[0].ID < my.leader
			]
			send[my.right == zone]<pending[0].ID,pending[0].SEED,(pending[0].COUNT)>{
				my.leader = pending[0].ID;
				pending = removeFirstElement(pending);
			}.FOLLOWER
			
			+
			
			[
				(size(my.pending) > 0) && 
				(pending[0].SEED == my.leaderSeed) && 
				pending[0].ID > my.leader
			]
			send[my.right == zone]<pending[0].ID,pending[0].SEED,(pending[0].COUNT)>{
				pending = removeFirstElement(pending);
			}.FOLLOWER;
		
	}
	init{
		IDLE
	}
}




system LeaderElectionInRing {	
	collective {
		for (i ; i < NODEZ ; 1) {
			new Agent(i,RND);
		}
	}	
	environment {
		store {
			attrib int messages = 0;
			}
		rate {
			activate* {
				return 1;
//				return 0.8;
//				return 0.5;
//				return 0.3;
			}
		}
		update {
			send{
				messages = global.messages+1;
			}
		}
	}
}

measure leader = #{Agent[LEADER] | my.sizy == NODEZ};
measure followerz = #{Agent[FOLLOWER] | true};
measure messages = global.messages;
measure idlez = #{Agent[IDLE] | true};
measure activez = #{Agent[ACTIVE] | true};
measure checkz = #{Agent[CHECK] | true};

//measure activeIdlez = #{Agent[ACTIVEIDLE] | true};
