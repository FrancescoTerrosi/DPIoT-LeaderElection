
record ToForward = [int ID, int COUNT];

fun list<ToForward> removeFirstElement(list<ToForward> pending){
	list<ToForward> result = newList(ToForward);
	if (size(pending) == 1){
		return result;
	}
	for i from 1 to size(pending){
		result = result + [:pending[i]:];
	}
	return result;
}

fun ToForward addElement(int id, int counter){
	ToForward result := [ID := id,COUNT := counter];
	return result;
}

const NODEZ = 50;


component Agent(int zone){
	store{
		attrib int id = zone;
		attrib int candidatez = 1;
		attrib int sizy = 1;
		attrib zone := zone;
		attrib right := (zone+1)%NODEZ;
		attrib left := (zone+NODEZ-1)%NODEZ;
		attrib list<ToForward> pending = newList(ToForward);
		attrib int leader = id;
		attrib bool known = false;
		attrib bool done = false;
		
	}
	behaviour{
		IDLE = 
			send[my.right == zone]<my.id,my.candidatez>{}.ACTIVE
			
			+
			
			send[my.left == zone](anId,aCounter){
				pending = pending +[:addElement(anId,aCounter):];
			}.FOLLOWER;
		
		
		ACTIVE =
		
		send[my.left == zone](anId,aCounter){
			pending = pending +[:addElement(anId,aCounter):];
		}.ACTIVE
		
		+
		
		[(size(my.pending) > 0) && (pending[0].ID == my.id)]check*{
			my.known = true;
		}.CHECK
		
		+
		
		[(size(my.pending) > 0) && (pending[0].ID != my.id)]send[my.right == zone]<pending[0].ID,(pending[0].COUNT+1)>{
			my.leader = min(pending[0].ID,my.leader);
			my.candidatez = my.candidatez+1;
			pending = removeFirstElement(pending);
		}.ACTIVE;
		
		
		CHECK = 
			[my.candidatez == my.pending[0].COUNT && my.leader == my.id]becomeLeader*{
				pending = removeFirstElement(pending);
			}.LEADER
			
			+
			
			[my.candidatez == my.pending[0].COUNT && my.leader != my.id]becomeFollower*{
				
			}.FOLLOWER
			
			+
			
			[my.candidatez != my.pending[0].COUNT]becomeFeederz*{}.ACTIVE
			
			+
			
		send[my.left == zone](anId,aCounter){
			pending = pending +[:addElement(anId,aCounter):];
		}.CHECK;
			
			
		LEADER = 

			count[my.right == zone]<my.id,my.sizy>{}.LEADER
			
			+
			
			count[my.left == zone](anId,aCounter){
				my.sizy = aCounter;
			}.LEADER;
		
		FOLLOWER =
		
			count[my.left == zone](anId,aCounter){
				my.leader = min(my.leader,anId);
				pending = pending +[:addElement(anId,aCounter):];
			}.FOLLOWER
			
			+
			
			[(size(my.pending) > 0) && pending[0].ID == my.leader]count<pending[0].ID, pending[0].COUNT+1>{
				pending = removeFirstElement(pending);
			}.FOLLOWER
			
			+
		
			[(size(my.pending) > 0)]send[my.right == zone]<pending[0].ID,(pending[0].COUNT)>{
				my.leader = min(pending[0].ID,my.leader);
				pending = removeFirstElement(pending);
			}.FOLLOWER
			
			+
			
			send[my.left == zone](anId,aCounter){
				pending = pending +[:addElement(anId,aCounter):];
			}.FOLLOWER;
			
	}
	init{
		IDLE
	}
}




system AllTheWayInRing {	
	collective {
		for (i ; i < NODEZ ; 1) {
			new Agent(i);
		}
	}	
	environment {
		store {
			attrib int messages = 0;
			}
		update {
			send{
				messages = global.messages+1;
			}
		}
	}
}

measure leader = #{Agent[LEADER] | true};
measure followerz = #{Agent[FOLLOWER] | true};
measure messages = global.messages;
measure idlez = #{Agent[IDLE] | true};
measure activez = #{Agent[ACTIVE] | true};
measure checkz = #{Agent[CHECK] | true};

